rule align_reads:
	output:
		sam = temp( "results/aligned/tmp/{ID}.bwamem.sam" )
	input:
		fq1 = lambda wildcards: get_fastq_files( wildcards.ID )['read1'],
		fq2 = lambda wildcards: get_fastq_files( wildcards.ID )['read2'],
		reference = config["reference"],
		index = "%s.bwt" % config["reference"]
	params:
		read_group_line = lambda wildcards: get_read_group_line( wildcards.ID )
	shell: """
		bwa mem -o {output.sam} -a -R "{params.read_group_line}" {input.reference} {input.fq1} {input.fq2}
	"""

rule fix_matepair_information_and_convert_to_bam:
	output:
		bam = temp( "results/aligned/tmp/{ID}.fixmate.bam" )
	input:
		sam = rules.align_reads.output.sam
	shell: """
		samtools fixmate -m {input.sam} {output.bam}
	"""

rule sort_reads_by_position:
	output:
		bam = temp( "results/aligned/tmp/{ID}.sorted.bam" )
	input:
		bam = rules.fix_matepair_information_and_convert_to_bam.output.bam
	shell: """
		samtools sort -o {output.bam} {input.bam}
	"""

rule mark_duplicate_read_pairs:
	output:
		bam = temp( "results/aligned/tmp/{ID}.bam" )
	input:
		bam = rules.sort_reads_by_position.output.bam
	shell: """
		samtools markdup {input.bam} {output.bam}
	"""

rule index_reads:
	output:
		bai = temp( "%s.bai" % rules.mark_duplicate_read_pairs.output.bam )
	input:
		bam = rules.mark_duplicate_read_pairs.output.bam
	shell: """
		samtools index {input.bam}
	"""

rule rename_alignment_files:
	output:
		bam = "results/aligned/{name}.bam",
		bai = "results/aligned/{name}.bam.bai"
	input:
		bam = lambda wildcards: "results/aligned/tmp/{ID}.bam".format( ID = find_sample_with_name( wildcards.name )['ID'] ),
		bai = lambda wildcards: "results/aligned/tmp/{ID}.bam.bai".format( ID = find_sample_with_name( wildcards.name )['ID'] )
	shell: """
		mv {input.bam} {output.bam}
		mv {input.bai} {output.bai}
	"""
